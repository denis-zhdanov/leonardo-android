plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'com.jfrog.bintray' version '1.8.4'
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode rootProject.ext.version
        versionName "${rootProject.ext.version}.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.1'
    testImplementation 'org.assertj:assertj-core:3.12.1'
}

if (!System.getenv('CI_ENV')) {
    apply plugin: 'signing'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/dokkaJavadoc"
    noStdlibLink = false
    noJdkLink = false
}

task kotlindocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from "$buildDir/dokkaJavadoc"
}

artifacts {
    add("archives", kotlindocJar)
    add("archives", sourceJar)
}

if (!System.getenv('CI_ENV')) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (!System.getenv('CI_ENV')) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
                        password: project.hasProperty('ossrhPassword') ? ossrhPassword : '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
                        password: project.hasProperty('ossrhPassword') ? ossrhPassword : '')
            }

            pom.project {
                packaging 'jar'

                name 'Leonardo'
                description 'A chart drawing library for Android'
                url 'https://github.com/denis-zhdanov/leonardo-android'

                scm {
                    connection 'scm:git:git://github.com/denis-zhdanov/leonardo-android'
                    developerConnection 'scm:git:git://github.com/denis-zhdanov/leonardo-android'
                    url 'https://github.com/denis-zhdanov/leonardo-android'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'denis'
                        name 'Denis Zhdanov'
                        email 'denzhdanov@gmail.com'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact("$buildDir/outputs/aar/leonardo-release.aar")
            artifact kotlindocJar
            artifact sourceJar
            groupId 'tech.harmonysoft'
            artifactId archivesBaseName
            version rootProject.ext.version
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['MyPublication']

    pkg {

        repo = 'harmonysoft.tech'
        name = archivesBaseName
        licenses = ['MIT']
        vcsUrl = 'https://github.com/denis-zhdanov/leonardo-android'

        version {
            name = "${rootProject.ext.version}"
            released  = new Date()
        }
    }
}